entity Permission {
    User: User
    Level: Int
}
        
entity User {
    UserName: @String
    PassWord: String
    Age: Int
}

entity Token {
    ForUser: User
    Token: @String
} 
        
entity Goods {
    Owner: User
    Name: String
    Price: Int
    Stock: Int      
    Description: String
}
        
entity OrderItem {
    Goods: Goods
    Count: Int
}

entity Order {
    Buyer: User
    Items: [OrderItem]
    TotalPrice: Int
}
        

fn permissionCheck(uid: User.Id, requireLevel: Int) -> Bool {
    return Permission.filter(u => u.User.Id.eq(uid)).first().Level.ge(requireLevel)
}

fn setPermissLevel(uid: User.Id, level: Int) {
    let p = Permission.filter(p => p.User.Id.eq(uid))
    let cnt = p.count()
    cnt.eq(0).cond(() => {
        Permission.append(Permission.new({
            User: uid.User,
            Level: level
        }))
    }, () => {
        p.first().Level.set(level)
    })
}        
        
fn signup(u: User) {
    User.append(u)
}

fn signdown(uid: User.Id) {
    User.remove(u => u.Id.eq(uid))
}

fn uuid() -> String

fn login(name: User.UserName, pw: User.PassWord) -> Token.Token {
    name.User.PassWord.eq(pw).assert("用户名或密码不正确")

    let newTok = uuid().pin()
    Token.append(Token.new({
        ForUser: name.User,
        Token: newTok
    }))
    return newTok
}

fn logout(tok: Token.Token) {
    Token.remove(t => t.Token.eq(tok))
}

fn tokenToUser(tok: Token.Token) -> User {
    return tok.Token.ForUser
}
           
fn getAllGoods() -> [Goods] {
    return Goods
}

fn newGoods(g: Goods) {
    Goods.append(g)
}

fn removeGoods(gid: Goods.Id) {
    Goods.remove(g => g.Id.eq(gid))
}

fn updateGoods(gid: Goods.Id, g: Goods) {
    gid.Goods.set(g)
}
        
fn newOrder(buyerId: User.Id) {
    Order.append(Order.new({
        Buyer: buyerId.User,
        TotalPrice: 0
    }))
}

fn removeOrder(oid: Order.Id) {
    Order.remove(o => o.Id.eq(oid))
}

fn getAllOrder() -> [Order] {
    return Order
}

fn getOrderItems(oid: Order.Id) -> [OrderItem] {
    return oid.Order.Items
}

fn getOnesOrder(uid: User.Id) -> [Order] {
    return Order.filter(o => o.Buyer.Id.eq(uid))
}
